let
  options_module =
    _file: excluded: is_top:
    {
      config,
      options,
      wlib,
      lib,
      mainConfig ? null,
      mainOpts ? null,
      ...
    }@top:
    {
      inherit _file;
      options.${if !(excluded.argv0type or false) then "argv0type" else null} = lib.mkOption {
        type =
          with lib.types;
          either (enum [
            "resolve"
            "inherit"
          ]) (functionTo str);
        default = if mainConfig != null && config.mirror or false then mainConfig.argv0type else "inherit";
        description = ''
          `argv0` overrides this option if not null or unset

          Both `shell` and the `nix` implementations
          ignore this option, as the shell always resolves `$0`

          However, the `binary` implementation will use this option

          Values:

          - `"inherit"`:

          The executable inherits argv0 from the wrapper.
          Use instead of `--argv0 '$0'`.

          - `"resolve"`:

          If argv0 does not include a "/" character, resolve it against PATH.

          - Function form: `str -> str`

          This one works only in the nix implementation. The others will treat it as `inherit`

          Rather than calling exec, you get the command plus all its flags supplied,
          and you can choose how to run it.

          e.g. `command_string: "eval \"$(''${command_string})\";`

          It will also be added to the end of the overall `DAL`,
          with the name `NIX_RUN_MAIN_PACKAGE`

          Thus, you can make things run after it,
          but by default it is still last.
        '';
      };
      options.${if !(excluded.argv0 or false) then "argv0" else null} = lib.mkOption {
        type = lib.types.nullOr lib.types.str;
        default = if mainConfig != null && config.mirror or false then mainConfig.argv0 else null;
        description = ''
          --argv0 NAME

          Set the name of the executed process to NAME.
          If unset or null, defaults to EXECUTABLE.

          overrides the setting from `argv0type` if set.
        '';
      };
      options.${if !(excluded.unsetVar or false) then "unsetVar" else null} = lib.mkOption {
        type = wlib.types.dalWithEsc lib.types.str;
        default = if mainConfig != null && config.mirror or false then mainConfig.unsetVar else [ ];
        description = ''
          --unset VAR

          Remove VAR from the environment.
        '';
      };
      options.${if !(excluded.runShell or false) then "runShell" else null} = lib.mkOption {
        type = wlib.types.dalWithEsc wlib.types.stringable;
        default = if mainConfig != null && config.mirror or false then mainConfig.runShell else [ ];
        description = ''
          --run COMMAND

          Run COMMAND before executing the main program.

          This option takes a list.

          Any entry can instead be of type `{ data, name ? null, before ? [], after ? [], esc-fn ? null }`

          This will cause it to be added to the DAG.

          If no name is provided, it cannot be targeted.
        '';
      };
      options.${if !(excluded.chdir or false) then "chdir" else null} = lib.mkOption {
        type = wlib.types.dalWithEsc wlib.types.stringable;
        default = if mainConfig != null && config.mirror or false then mainConfig.chdir else [ ];
        description = ''
          --chdir DIR

          Change working directory before running the executable.
          Use instead of `--run "cd DIR"`.
        '';
      };
      options.${if !(excluded.addFlag or false) then "addFlag" else null} = lib.mkOption {
        type = wlib.types.wrapperFlag;
        default = if mainConfig != null && config.mirror or false then mainConfig.addFlag else [ ];
        example = lib.literalMD ''
          ```nix
          [
            "-v"
            "-f"
            [
              "--config"
              ./storePath.cfg
            ]
            [
              "-s"
              "idk"
            ]
          ]
          ```
        '';
        description = ''
          Wrapper for

          --add-flag ARG

          Prepend the single argument ARG to the invocation of the executable,
          before any command-line arguments.

          This option takes a list. To group them more strongly,
          option may take a list of lists as well.

          Any entry can instead be of type `{ data, name ? null, before ? [], after ? [], esc-fn ? null }`

          This will cause it to be added to the DAG.

          If no name is provided, it cannot be targeted.
        '';
      };
      options.${if !(excluded.appendFlag or false) then "appendFlag" else null} = lib.mkOption {
        type = wlib.types.wrapperFlag;
        default = if mainConfig != null && config.mirror or false then mainConfig.appendFlag else [ ];
        example = lib.literalMD ''
          ```nix
          [
            "-v"
            "-f"
            [
              "--config"
              ./storePath.cfg
            ]
            [
              "-s"
              "idk"
            ]
          ]
          ```
        '';
        description = ''
          --append-flag ARG

          Append the single argument ARG to the invocation of the executable,
          after any command-line arguments.

          This option takes a list. To group them more strongly,
          option may take a list of lists as well.

          Any entry can instead be of type `{ data, name ? null, before ? [], after ? [], esc-fn ? null }`

          This will cause it to be added to the DAG.

          If no name is provided, it cannot be targeted.
        '';
      };
      options.${if !(excluded.prefixVar or false) then "prefixVar" else null} = lib.mkOption {
        type = wlib.types.wrapperFlags 3;
        default = if mainConfig != null && config.mirror or false then mainConfig.prefixVar else [ ];
        example = lib.literalMD ''
          ```nix
          [
            [
              "LD_LIBRARY_PATH"
              ":"
              "''${lib.makeLibraryPath (with pkgs; [ ... ])}"
            ]
            [
              "PATH"
              ":"
              "''${lib.makeBinPath (with pkgs; [ ... ])}"
            ]
          ]
          ```
        '';
        description = ''
          --prefix ENV SEP VAL

          Prefix ENV with VAL, separated by SEP.
        '';
      };
      options.${if !(excluded.suffixVar or false) then "suffixVar" else null} = lib.mkOption {
        type = wlib.types.wrapperFlags 3;
        default = if mainConfig != null && config.mirror or false then mainConfig.suffixVar else [ ];
        example = lib.literalMD ''
          ```nix
          [
            [
              "LD_LIBRARY_PATH"
              ":"
              "''${lib.makeLibraryPath (with pkgs; [ ... ])}"
            ]
            [
              "PATH"
              ":"
              "''${lib.makeBinPath (with pkgs; [ ... ])}"
            ]
          ]
          ```
        '';
        description = ''
          --suffix ENV SEP VAL

          Suffix ENV with VAL, separated by SEP.
        '';
      };
      options.${if !(excluded.prefixContent or false) then "prefixContent" else null} = lib.mkOption {
        type = wlib.types.wrapperFlags 3;
        default = if mainConfig != null && config.mirror or false then mainConfig.prefixContent else [ ];
        description = ''
          ```nix
          [
            [ "ENV" "SEP" "FILE" ]
          ]
          ```

          Prefix ENV with contents of FILE and SEP at build time.

          Also accepts sets like the other options

          ```nix
          [
            [ "ENV" "SEP" "FILE" ]
            { data = [ "ENV" "SEP" "FILE" ]; esc-fn = lib.escapeShellArg; /* name, before, after */ }
          ]
          ```
        '';
      };
      options.${if !(excluded.suffixContent or false) then "suffixContent" else null} = lib.mkOption {
        type = wlib.types.wrapperFlags 3;
        default = if mainConfig != null && config.mirror or false then mainConfig.suffixContent else [ ];
        description = ''
          ```nix
          [
            [ "ENV" "SEP" "FILE" ]
          ]
          ```

          Suffix ENV with SEP and then the contents of FILE at build time.

          Also accepts sets like the other options

          ```nix
          [
            [ "ENV" "SEP" "FILE" ]
            { data = [ "ENV" "SEP" "FILE" ]; esc-fn = lib.escapeShellArg; /* name, before, after */ }
          ]
          ```
        '';
      };
      options.${if !(excluded.flags or false) then "flags" else null} = lib.mkOption {
        type =
          with lib.types;
          (
            wlib.types.dagOf
            // {
              dontConvertFunctions = true;
              modules = wlib.types.dagWithEsc.modules ++ [
                {
                  options.sep = lib.mkOption {
                    type = nullOr str;
                    default = null;
                    description = ''
                      A per-item override of the default separator used for flags and their values
                    '';
                  };
                  options.ifs = lib.mkOption {
                    type = nullOr str;
                    default = null;
                    description = ''
                      If `null`, and a list is provided, the key-value pairs will be repeated.

                      If a string is provided, it will instead be the key, followed by the main separator,
                      followed by the list joined with this value as the separator.


                      `flags."--myflag" = { ifs = null; sep = "="; data = [ "a" "b" "c" ]; }`

                      will result in

                      `--myflag=a --myflag=b --myflag=c`

                      `flags."--myflag" = { ifs = ","; sep = "="; data = [ "a" "b" "c" ]; }`

                      will result in

                      `--myflag=a,b,c`
                    '';
                  };
                }
              ];
            }
          )
            (
              nullOr (oneOf [
                bool
                wlib.types.stringable
                (listOf wlib.types.stringable)
              ])
            );
        default = if mainConfig != null && config.mirror or false then mainConfig.flags else { };
        example = lib.literalMD ''
          ```nix
          {
            "--config" = ./nixPath;
          }
          ```
        '';
        description = ''
          Flags to pass to the wrapper.
          The key is the flag name, the value is the flag value.
          If the value is true, the flag will be passed without a value.
          If the value is false or null, the flag will not be passed.
          If the value is a list, the flag will be passed multiple times with each value.

          This option takes a set.

          Any entry can instead be of type `{ data, before ? [], after ? [], esc-fn ? null, sep ? null, ifs ? null }`

          The `sep` field may be used to override the value of `config.flagSeparator`

          The `ifs` field is relevant when your value is a list.

          `flags."--myflag" = { ifs = null; sep = "="; data = [ "a" "b" "c" ]; }`

          will result in

          `--myflag=a --myflag=b --myflag=c`

          `flags."--myflag" = { ifs = ","; sep = "="; data = [ "a" "b" "c" ]; }`

          will result in

          `--myflag=a,b,c`
        '';
      };
      options.${if !(excluded.flagSeparator or false) then "flagSeparator" else null} = lib.mkOption {
        type = lib.types.nullOr lib.types.str;
        default = if mainConfig != null && config.mirror or false then mainConfig.flagSeparator else null;
        description = ''
          Separator between flag names and values when generating args from flags.
          `" "` for `--flag value` or `"="` for `--flag=value`

          If null (the default), they will always be separate, sequential arguments,
          even if not interpolated by a shell (such as with the `"binary"` implementation)
        '';
      };
      options.${if !(excluded.extraPackages or false) then "extraPackages" else null} = lib.mkOption {
        type = lib.types.listOf lib.types.package;
        default = if mainConfig != null && config.mirror or false then mainConfig.extraPackages else [ ];
        description = ''
          Additional packages to add to the wrapper's runtime PATH.
          This is useful if the wrapped program needs additional libraries or tools to function correctly.

          Adds all its entries to the DAG under the name `NIX_PATH_ADDITIONS`
        '';
      };
      options.${if !(excluded.runtimeLibraries or false) then "runtimeLibraries" else null} =
        lib.mkOption
          {
            type = lib.types.listOf lib.types.package;
            default = if mainConfig != null && config.mirror or false then mainConfig.runtimeLibraries else [ ];
            description = ''
              Additional libraries to add to the wrapper's runtime LD_LIBRARY_PATH.
              This is useful if the wrapped program needs additional libraries or tools to function correctly.

              Adds all its entries to the DAG under the name `NIX_LIB_ADDITIONS`
            '';
          };
      config.${
        if excluded.extraPackages or false && excluded.runtimeLibraries or false then null else "suffixVar"
      } =
        lib.optional (config.extraPackages or [ ] != [ ]) {
          name = "NIX_PATH_ADDITIONS";
          data = [
            "PATH"
            ":"
            "${lib.makeBinPath config.extraPackages}"
          ];
        }
        ++ lib.optional (config.runtimeLibraries or [ ] != [ ]) {
          name = "NIX_LIB_ADDITIONS";
          data = [
            "LD_LIBRARY_PATH"
            ":"
            "${lib.makeLibraryPath config.runtimeLibraries}"
          ];
        };
      options.${if !(excluded.env or false) then "env" else null} = lib.mkOption {
        type = wlib.types.dagWithEsc (lib.types.nullOr wlib.types.stringable);
        default = if mainConfig != null && config.mirror or false then mainConfig.env else { };
        example = {
          "XDG_DATA_HOME" = "/somewhere/on/your/machine";
        };
        description = ''
          Environment variables to set in the wrapper.

          This option takes a set.

          Any entry can instead be of type `{ data, before ? [], after ? [], esc-fn ? null }`

          This will cause it to be added to the DAG,
          which will cause the resulting wrapper argument to be sorted accordingly
        '';
      };
      options.${if !(excluded.envDefault or false) then "envDefault" else null} = lib.mkOption {
        type = wlib.types.dagWithEsc (lib.types.nullOr wlib.types.stringable);
        default = if mainConfig != null && config.mirror or false then mainConfig.envDefault else { };
        example = {
          "XDG_DATA_HOME" = "/only/if/not/set";
        };
        description = ''
          Environment variables to set in the wrapper.

          Like env, but only adds the variable if not already set in the environment.

          This option takes a set.

          Any entry can instead be of type `{ data, before ? [], after ? [], esc-fn ? null }`

          This will cause it to be added to the DAG,
          which will cause the resulting wrapper argument to be sorted accordingly
        '';
      };
      options.${if !(excluded.escapingFunction or false) then "escapingFunction" else null} =
        lib.mkOption
          {
            type = lib.types.functionTo lib.types.str;
            default =
              if mainConfig != null && config.mirror or false then
                mainConfig.escapingFunction
              else
                lib.escapeShellArg;
            defaultText = lib.literalExpression "lib.escapeShellArg";
            description = ''
              The function to use to escape shell values

              Caution: When using `shell` or `binary` implementations,
              these will be expanded at BUILD time.

              You should probably leave this as is when using either of those implementations.

              However, when using the `nix` implementation, they will expand at runtime!
              Which means `wlib.escapeShellArgWithEnv` may prove to be a useful substitute!
            '';
          };
      options.${if !(excluded.wrapperImplementation or false) then "wrapperImplementation" else null} =
        lib.mkOption
          {
            type = lib.types.enum [
              "nix"
              "shell"
              "binary"
            ];
            default =
              if mainConfig != null && config.mirror or false then mainConfig.wrapperImplementation else "nix";
            description = ''
              the `nix` implementation is the default

              It makes the `escapingFunction` most relevant.

              This is because the `shell` and `binary` implementations
              use `pkgs.makeWrapper` or `pkgs.makeBinaryWrapper`,
              and arguments to these functions are passed at BUILD time.

              So, generally, when not using the nix implementation,
              you should always prefer to have `escapingFunction`
              set to `lib.escapeShellArg`.

              However, if you ARE using the `nix` implementation,
              using `wlib.escapeShellArgWithEnv` will allow you
              to use `$` expansions, which will expand at runtime.

              `binary` implementation is useful for programs
              which are likely to be used in "shebangs",
              as macos will not allow scripts to be used for these.

              However, it is more limited. It does not have access to
              `runShell`, `prefixContent`, and `suffixContent` options.

              Chosing `binary` will thus cause values in those options to be ignored.
            '';
          };
      config._module.args = {
        mainConfig = null;
        mainOpts = null;
      };
      options.${if !(excluded.wrapperVariants or false) && is_top then "wrapperVariants" else null} =
        lib.mkOption
          {
            default = { };
            description = ''
              Allows for you to apply the wrapper options to multiple binaries from config.package (or elsewhere)

              They are called variants because they are the same options as the top level makeWrapper options,
              however, their defaults mirror the values of the top level options.

              Meaning if you set `config.env.MYVAR = "HELLO"` at the top level,
              then the following statement would be true by default:

              `config.wrapperVariants.foo.env.MYVAR.data == "HELLO"`

              They achieve this by receiving `mainConfig` and `mainOpts` via `specialArgs`,
              which contain `config` and `options` from the top level.
            '';
            type = lib.types.attrsOf (
              lib.types.submoduleWith {
                specialArgs = {
                  mainConfig = config;
                  mainOpts = options;
                  inherit wlib;
                };
                modules = [
                  (options_module _file excluded false)
                  (
                    { name, config, ... }:
                    {
                      inherit _file;
                      options.enable = lib.mkOption {
                        type = lib.types.bool;
                        default = true;
                        description = ''
                          Enables the wrapping of this variant
                        '';
                      };
                      options.mirror = lib.mkOption {
                        type = lib.types.bool;
                        default = true;
                        description = ''
                          Allows the variant to inherit defaults from the top level
                        '';
                      };
                      options.exePath = lib.mkOption {
                        type = lib.types.nullOr wlib.types.nonEmptyLine;
                        default = "${top.config.binDir}/${name}";
                        description = ''
                          The location within the package of the thing to wrap.
                        '';
                      };
                      options.binName = lib.mkOption {
                        type = wlib.types.nonEmptyLine;
                        default = name;
                        description = ''
                          The name of the file to output to `''${placeholder config.outputName}''${config.wrapperPaths.relDir}`
                        '';
                      };
                      options.binDir = lib.mkOption {
                        type = lib.types.nullOr wlib.types.nonEmptyLine;
                        default = top.config.binDir or "bin";
                        description = ''
                          the directory the wrapped result will be placed into, with the name indicated by the `binName` option

                          i.e. `"''${placeholder outputName}/<THIS_VALUE>/''${binName}"`
                        '';
                      };
                      options.outputName = lib.mkOption {
                        type = wlib.types.nonEmptyLine;
                        default = top.config.outputName or "out";
                        description = ''
                          The derivation output the wrapped binary will be placed into.
                        '';
                      };
                      options.wrapperPaths = {
                        input = lib.mkOption {
                          type = lib.types.str;
                          readOnly = true;
                          default = "${config.package}" + lib.optionalString (config.exePath != null) "/${config.exePath}";
                          description = "
                            The path which is to be wrapped by the wrapperFunction implementation
                          ";
                        };
                        placeholder = lib.mkOption {
                          type = lib.types.str;
                          readOnly = true;
                          default = "${placeholder config.outputName or "out"}${config.wrapperPaths.relPath}";
                          description = "
                            The path which the wrapperFunction implementation is to output its result to.
                          ";
                        };
                        relPath = lib.mkOption {
                          type = lib.types.str;
                          readOnly = true;
                          default =
                            config.wrapperPaths.relDir + lib.optionalString (config.binName != "") "/${config.binName}";
                          description = ''
                            The binary will be output to `''${placeholder config.outputName}''${config.wrapperPaths.relPath}`
                          '';
                        };
                        relDir = lib.mkOption {
                          type = lib.types.str;
                          readOnly = true;
                          default = lib.optionalString (config.binDir != null) "/${config.binDir}";
                          description = ''
                            The binary will be output to `''${placeholder config.outputName}''${config.wrapperPaths.relDir}/''${config.binName}`
                          '';
                        };
                      };
                      options.package = lib.mkOption {
                        type = wlib.types.stringable;
                        default = top.config.package;
                        description = ''
                          The package to wrap with these options
                        '';
                      };
                    }
                  )
                ];
              }
            );
          };
    };
  deprecationMessage =
    name:
    (builtins.warn or builtins.trace) ''
      WARNING: `(import wlib.modules.makeWrapper).${name}` is deprecated

      It has been moved to `wlib.makeWrapper.${name}`
    '';
  error_message = "this function has been moved to `wlib.makeWrapper` and also requires `pkgs` or `callPackage` to be provided to it";
in
{
  wrapAll =
    {
      wlib,
      config,
      pkgs ? null,
      callPackage ? pkgs.callPackage or (throw error_message),
      ...
    }@args:
    (deprecationMessage "wrapAll") wlib.makeWrapper.wrapAll (args // { inherit config callPackage; });
  wrapMain =
    {
      wlib,
      config,
      pkgs ? null,
      callPackage ? pkgs.callPackage or (throw error_message),
      ...
    }@args:
    (deprecationMessage "wrapMain") wlib.makeWrapper.wrapMain (args // { inherit config callPackage; });
  wrapVariants =
    {
      wlib,
      config,
      pkgs ? null,
      callPackage ? pkgs.callPackage or (throw error_message),
      ...
    }@args:
    (deprecationMessage "wrapperVariants") wlib.makeWrapper.wrapVariants (
      args // { inherit config callPackage; }
    );
  wrapVariant =
    {
      wlib,
      config,
      pkgs ? null,
      callPackage ? pkgs.callPackage or (throw error_message),
      ...
    }@args:
    (deprecationMessage "wrapVariant") wlib.makeWrapper.wrapVariant (
      args // { inherit config callPackage; }
    );

  wrapperFunction = null;
  excluded_options = { };
  exclude_wrapper = false;
  exclude_meta = false;
  _file = ./module.nix;
  key = ./module.nix;
  __functor =
    self:
    {
      config,
      lib,
      wlib,
      pkgs,
      # NOTE: makes sure builderFunction and wrapperFunction get name from _module.args
      options,
      extendModules,
      name ? null,
      ...
    }@args:
    {
      _file = self._file or ./module.nix;
      ${if (self.key or ./module.nix) != null then "key" else null} = self.key or ./module.nix;
      imports = [ (options_module (self._file or ./module.nix) (self.excluded_options or { }) true) ];
      options.${
        if
          builtins.isBool (self.excluded_options.wrapperFunction or null)
          && self.excluded_options.wrapperFunction
        then
          null
        else
          "wrapperFunction"
      } =
        lib.mkOption {
          type = lib.types.functionTo lib.types.str;
          default =
            if self.wrapperFunction or null != null then self.wrapperFunction else wlib.makeWrapper.wrapAll;
          description = ''
            Arguments:

            This option takes a function receiving the following arguments:

            module arguments + `pkgs.callPackage`

            ```
            {
              config,
              wlib,
              ... # <- anything you can get from pkgs.callPackage
            }
            ```
            This is the function used to process the wrapper arguments.

            By default, it is `wlib.makeWrapper.wrapAll`

            It will be called with the normal module arguments + `pkgs.callPackage` arguments

            The module calls it, and places the result in `config.buildCommand.makeWrapper` with `lib.mkOptionDefault` priority.

            The relative path to the thing to wrap is `config.wrapperPaths.input`

            This function is to return a string of build commands which create a result at `config.wrapperPaths.placeholder`
          '';
        };
      config.${if self.exclude_wrapper or false then null else "buildCommand"}.makeWrapper = {
        before = [ "symlinkScript" ];
        data = lib.mkOptionDefault (
          let
            res = pkgs.callPackage (
              if
                builtins.isBool (self.excluded_options.wrapperFunction or null)
                && self.excluded_options.wrapperFunction
              then
                wlib.makeWrapper.wrapAll
              else
                config.wrapperFunction
            ) args;
          in
          if builtins.isString res then
            res
          else
            throw ''
              Returning something other than a build command string from wrapperFunction is no longer supported.

              To pass other values to `builderFunction`, place them in an option.
            ''
        );
      };
      config.${if self.exclude_meta or false then null else "meta"} = {
        maintainers = [ wlib.maintainers.birdee ];
        description = {
          pre = ''
            An implementation of the `makeWrapper` interface via type safe module options.

            Allows you to choose one of several underlying implementations of the `makeWrapper` interface.

            Imported by `wlib.modules.default`

            Wherever the type includes `DAG` you can mentally substitute this with `attrsOf`

            Wherever the type includes `DAL` or `DAG list` you can mentally substitute this with `listOf`

            However they also take items of the form `{ data, name ? null, before ? [], after ? [] }`

            This allows you to specify that values are added to the wrapper before or after another value.

            The sorting occurs across ALL the options, thus you can target items in any `DAG` or `DAL` within this module from any other `DAG` or `DAL` option within this module.

            The `DAG`/`DAL` entries in this module also accept an extra field, `esc-fn ? null`

            If defined, it will be used instead of the value of `options.escapingFunction` to escape that value.

            It also has a set of submodule options under `config.wrapperVariants` which allow you
            to duplicate the effects to other binaries from the package, or add extra ones.

            Each one contains an `enable` option, and a `mirror` option.

            They also contain the same options the top level module does, however if `mirror` is `true`,
            as it is by default, then they will inherit the defaults from the top level as well.

            They also have their own `package`, `exePath`, and `binName` options, with sensible defaults.

            ---
          '';
          post = ''
            ---

            ## Modify this module before import

            Should you ever need to redefine `config.wrapperFunction`, you might have slightly different options!

            `makeWrapper = import wlib.modules.makeWrapper;`

            If you import it like shown, you gain the ability to modify it.

            You may `//` (update) the following values into the set you gain from importing the file:

            `exclude_wrapper = true;` to stop it from setting `config.buildCommand.makeWrapper`

            `wrapperFunction = ...;` to override the default `config.wrapperFunction` that it sets instead of excluding it.

            `exclude_meta = true;` to stop it from setting any values in `config.meta`

            `excluded_options = { ... };` where you may include `optionname = true`
            in order to not define that option.

            `_file` and `key`: `_file` changes the value set for the modules imported when you import this module. `key` is set on the main one if not `null`.

            In order to change these values, you change them in the set before importing the module like so:

            ```nix
              imports = [ (import wlib.modules.makeWrapper // { excluded_options.wrapperVariants = true; }) ];
            ```
          '';
        };
      };
    };
}
